%require "3.0"
%define api.pure full
%define api.token.prefix {TOK_}
%define parse.error verbose
%define parse.lac full

%code requires {
#include "ast.hxx"
#include <stdio.h>

using yyscan_t = void*;
}  // %code

%code provides {
int yylex_init(yyscan_t *yyscanner);
int yylex_destroy(yyscan_t yyscanner);
int yylex(YYSTYPE *yylval, yyscan_t yyscanner);
void *yyget_extra(yyscan_t yyscanner);
void  yyset_extra(void *user_defined, yyscan_t yyscanner);
void yyset_in(FILE *in, yyscan_t yyscanner);
void yyerror(yyscan_t yyscanner, igor::AST *ast, const char *msg);
}  // %code

%expect 0

%union {
  igor::AstConstant *constant;
  igor::AstFunction *function;
  igor::AstType *type;
  igor::AstArg *arg;
  igor::AstArgList *arg_list;
  string *name;
}

%param {yyscan_t yyscanner}
%parse-param {igor::AST *ast}

// Terminal symbols are in CAPS by convention.
%token <constant> CONSTANT
%token <name>     NAME
%token <name>     TYPE
%token            FN        // fn
%token            ARROW     // =>
%token            COLON     // :
%token            COMMA     // ,
%token            DOT       // .
%token            LB        // [
%token            RB        // ]
%token            LC        // {
%token            RC        // }
%token            LP        // (
%token            RP        // )
%token            WILDCARD  // _
%token            INVALID_CHAR

%type <function>  function
%type <name>      qualifier
%type <type>      type
%type <type>      base_type
%type <type>      qualified_type
%type <type>      type_spec
%type <type>      type_list
%type <arg>       arg
%type <arg_list>  arg_list
%type <arg_spec>  arg_spec

%destructor { delete $$; } <*>

%start module

%%

module:
    %empty
  | function module

function:
    FN NAME arg_spec ret_spec body
{
  $$ = new igor::AstFunction(std::move(*$2));
  delete $2;
  if (!ast->add_function($$)) {
    delete $$;
    YYABORT;
  }
}

body:
    LC RC

arg_spec:
    LP RP
  | LP arg_list RP

arg:
    type
  | NAME COLON type
{
  delete $1;
}
  | NAME COMMA arg
{
  delete $1;
}

arg_list:
    args
  | arg COMMA arg_list

ret_spec:
    %empty
  | COLON arg_list

qualifier:
    NAME DOT
{
  $$ = new string("/" + *$1 + "/");
  delete $1;
}
  | NAME DOT qualifier
{
  $$ = new string("/" + *$1 + *$3);
  delete $1;
  delete $3;
}

// Type spec grammar:

// Notation for array: Type[]. Suffix applied to the last shortest type.
// Notation for tuple: [Type1, Type2]
//
// Notation for function: T1 => T2 => RT
// This function takes two arguments of types T1 and T2 and return RT. Idea is
// taken from Haskell - uniform arg/return type. This notation is currying
// friendly.
//
// Use parenthesis to group:
//   T1 => (T2 => T3)[] => (T4 => T5)
// This function takes T1 and array of functions T2=>T3 and returns a function
// T4=>T5.
//
// Generic instantiation uses parens: Templ(T1, T2).
type:
    type_spec
{
  $$ = $1;

  // DEBUG:
  // cout<<"__ type: "<<$$->to_string()<<endl;
  delete $$;
  $$ = nullptr;
}

type_spec:
    base_type { $$ = $1; }
  | base_type ARROW type_spec
{
  if ($3->name == "/std/Function" && $3->carry) {
    $$ = $3;
    $$->args.push_back($1);
  } else {
    $$ = new igor::AstType("/std/Function");
    $$->args.push_back($3);
    $$->args.push_back($1);
  }
}

qualified_type:
    TYPE
{
  $$ = new igor::AstType(std::move(*$1));
  delete $1;
}
  | qualifier TYPE
{
  $$ = new igor::AstType(*$1 + *$2);
  delete $1;
  delete $2;
}

base_type:
    qualified_type { $$ = $1; }
  | qualified_type LP type_list RP  // Generic instance
{
  $$ = $3;
  if (!$$->name.empty()) {
    cerr<<"Internal error: type_list name is not empty"<<endl;
    YYABORT;
  }
  $$->name = std::move($1->name);
  delete $1;
}
  | LP type_spec RP  // Parens around type
{
  $$ = $2;
  if ($$->name == "/std/Function") {
    $$->carry = false;
  }
}
  | base_type LB RB  // Array, /std/Array
{
  $$ = new igor::AstType("/std/Array");
  $$->args.push_back($1);
}
  | LB type_list RB  // Tuple, /std/Tuple
{
  $$ = $2;
  if (!$$->name.empty()) {
    cerr<<"Internal error: type_list name is not empty"<<endl;
    YYABORT;
  }
  $$->name = "/std/Tuple";
}

 type_list:
     type_spec
{
  $$ = new igor::AstType("");
  $$->args.push_back($1);
}
   | type_spec COMMA type_list
{
  $$ = $3;
  $$->args.push_back($1);
}

%%

void yyerror(yyscan_t yyscanner, igor::AST *ast, const char *msg) {
  string s;
  ast->error(s.append("Parse error: ").append(msg));
}
